apply plugin: 'java'
apply plugin: 'idea'
apply from: "$rootDir/gradle/integrationTest.gradle"

repositories {
    mavenCentral()
}

def tomcatVersion = '8.5.6'

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
    compile "org.apache.tomcat:tomcat-jasper:${tomcatVersion}"

    compile 'org.springframework:spring-context:4.3.3.RELEASE'
    compile 'org.springframework:spring-web:4.3.3.RELEASE'
    compile 'org.springframework:spring-webmvc:4.3.3.RELEASE'

    testCompile 'junit:junit:4.12'
}

task copyDeps(type:Copy, dependsOn: assemble) {
    from configurations.compile
    into "${buildDir}/deps"
}

build.dependsOn copyDeps

task buildImage(type:Exec, dependsOn: copyDeps) {
    commandLine 'docker', 'build', '-t', project.name, '.'
}

task start(type:Exec, dependsOn: buildImage) {
    commandLine 'docker-compose',
            '-f', 'docker-compose.yml',
            '-f', 'docker-compose.dev.yml',
            'up', '-d','--no-build'
}

task stop(type:Exec) {
    commandLine 'docker-compose', 'down'
}

build.dependsOn buildImage